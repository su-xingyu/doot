// --- Serializer definition ---
.decl isSerializerMethod(?method:Method)
isSerializerMethod("<InlineA: java.lang.String toHexString(java.lang.String)>").

.decl SerializerInputParam(?method:Method, ?index:number)
SerializerInputParam("<InlineA: java.lang.String toHexString(java.lang.String)>", 0).

// serializerValue is treated as a special heap value to fit in the framework
.decl Serializer(?serializerValue:Value, ?input:Value, ?output:Var, ?method:Method)

.decl isSerializerValue(?serializerValue:Value)

// --- Deserializer definition ---
.decl isDeserializerMethod(?method:Method)
isDeserializerMethod("<InlineA: java.lang.String fromHexString(java.lang.String)>").

.decl DeserializerInputParam(?method:Method, ?index:number)
DeserializerInputParam("<InlineA: java.lang.String fromHexString(java.lang.String)>", 0).

.decl Deserializer(?input:Var, ?output:Var, ?method:Method)

// --- Inliner definition ---
.decl isInverse(?serializerMethod:Method, ?deserializerMethod:Method)
isInverse("<InlineA: java.lang.String toHexString(java.lang.String)>", "<InlineA: java.lang.String fromHexString(java.lang.String)>").

.decl CandidateContextPair(?serializerCtx:mainAnalysis.configuration.HContext, ?varCtx:mainAnalysis.configuration.Context)

.decl CandidateInvocationPair(?serializerInvocation:MethodInvocation, ?varInvocation:MethodInvocation)

.decl CandidateMethods(?serializerCaller:Method, ?serializerCallee:Method, ?varCaller:Method, ?varCallee:Method)

.decl MethodPairToInline(?caller:Method, ?callee:Method)
.output MethodPairToInline

// --- Rules ---
// Reuse mainAnalysis.VarPointsTo for context-sensitive analysis, use current call-site context as the heap context for
// registered ?serizlierValue
// Must register the type of ?serializerValue to make things work
mainAnalysis.Value_Type(?serializerValue, ?type),
Serializer(?serializerValue, ?input, ?output, ?inmethod),
isSerializerValue(?serializerValue),
mainAnalysis.configuration.RecordContextRequest(?ctx, ?serializerValue, ?output) :-
    isSerializerMethod(?inmethod),
    mainAnalysis.CallGraphEdge(?ctx, ?invocation, _, ?inmethod),
    AssignReturnValue(?invocation, ?output),
    SerializerInputParam(?inmethod, ?index),
    ActualParam(?index, ?invocation, ?input),
    Method_ReturnType(?inmethod, ?type),
    ?serializerValue = cat("<", cat(?inmethod, cat(": ", cat(?input, ">")))).

Deserializer(?input, ?output, ?inmethod) :-
    isDeserializerMethod(?inmethod),
    mainAnalysis.CallGraphEdge(_, ?invocation, _, ?inmethod),
    AssignReturnValue(?invocation, ?output),
    DeserializerInputParam(?inmethod, ?index),
    ActualParam(?index, ?invocation, ?input).

CandidateContextPair(?serializerCtx, ?deserializerCtx) :-
    isInverse(?serializerMethod, ?deserializerMethod),
    Serializer(?serializerValue, _, _, ?serializerMethod),
    Deserializer(?deserializerInput, _, ?deserializerMethod),
    mainAnalysis.VarPointsTo(?serializerCtx, ?serializerValue, ?deserializerCtx, ?deserializerInput).

// We also want to inline intermediate variables
CandidateContextPair(?serializerCtx, ?varCtx) :-
    CandidateContextPair(?serializerCtx, _),
    mainAnalysis.VarPointsTo(?serializerCtx, ?serializerValue, ?varCtx, _),
    isSerializerValue(?serializerValue).

CandidateInvocationPair(?serializerInvocation, ?varInvocation) :-
    CandidateContextPair(?serializerCtx, ?varCtx),
    ?serializerCtx = [?serializerInvocation],
    ?varCtx = [?varInvocation].

CandidateMethods(?serializerCaller, ?serializerCallee, ?varCaller, ?varCallee) :-
    CandidateInvocationPair(?serializerInvocation, ?varInvocation),
    Instruction_Method(?serializerInvocation, ?serializerCaller),
    MethodInvocation_Method(?serializerInvocation, ?serializerCallee),
    Instruction_Method(?varInvocation, ?varCaller),
    MethodInvocation_Method(?varInvocation, ?varCallee).

// Handle the case when either serializer or targeted variable is in main method
CandidateMethods(?serializerCaller, ?serializerCallee, "<<none>>", ?mainMethod) :-
    CandidateInvocationPair(?serializerInvocation, "<<immutable-context>>"),
    Instruction_Method(?serializerInvocation, ?serializerCaller),
    MethodInvocation_Method(?serializerInvocation, ?serializerCallee),
    basic.MainMethodDeclaration(?mainMethod).

CandidateMethods("<<none>>", ?mainMethod, ?varCaller, ?varCallee) :-
    CandidateInvocationPair("<<immutable-context>>", ?varInvocation),
    Instruction_Method(?varInvocation, ?varCaller),
    MethodInvocation_Method(?varInvocation, ?varCallee),
    basic.MainMethodDeclaration(?mainMethod).

// Since we also inline intermediate variables, we need to exclude the possibility of ?varCallee being
// deserializer itself
MethodPairToInline(?meetPoint, ?serializerCallee),
MethodPairToInline(?meetPoint, ?varCallee) :-
    CandidateMethods(?meetPoint, ?serializerCallee, ?meetPoint, ?varCallee),
    !isDeserializerMethod(?varCallee).

MethodPairToInline(?meetPoint, ?serializerCallee) :-
    CandidateMethods(?meetPoint, ?serializerCallee, _, ?meetPoint).

MethodPairToInline(?meetPoint, ?varCallee) :-
    CandidateMethods(_, ?meetPoint, ?meetPoint, ?varCallee),
    !isDeserializerMethod(?varCallee).
